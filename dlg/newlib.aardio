import win.ui;
import fonts.fontAwesome;
/*DSG{{*/
var winform = win.form(text="aardio form";right=1040;bottom=606)
winform.add(
btnGlobalInsall={cls="plus";text="全局安装";left=100;top=247;right=191;bottom=280;bgcolor=11580047;border={radius=5;width=1};notify=1;z=7};
btnProjectInstall={cls="plus";text="项目安装";left=283;top=248;right=374;bottom=281;border={radius=5;width=1};notify=1;z=8};
combobox={cls="combobox";left=94;top=139;right=404;bottom=165;items={};mode="dropdown";z=9};
editLibName={cls="plus";left=94;top=49;right=404;bottom=75;align="left";border={color=-16711681;width=1};editable=1;iconColor=255;iconStyle={align="right";font=LOGFONT(h=-19;name='FontAwesome');padding={top=2;right=5}};iconText='\uF05E';notify=1;tabstop=1;textPadding={left=5;top=5;right=25};z=11};
libVersion={cls="plus";left=94;top=93;right=403;bottom=119;align="left";border={color=-16711681;width=1};editable=1;iconColor=255;iconStyle={align="right";font=LOGFONT(h=-19;name='FontAwesome');padding={top=2;right=5}};iconText='\uF05E';notify=1;tabstop=1;textPadding={left=5;top=5;right=25};z=4};
plus={cls="plus";text="包名：";left=23;top=49;right=86;bottom=79;align="right";color=16776960;z=3};
plus2={cls="plus";text="选择类型：";left=9;top=136;right=87;bottom=166;align="right";color=16776960;z=10};
plus4={cls="plus";text="版本：";left=24;top=93;right=87;bottom=123;align="right";color=16776960;z=6};
textLibName={cls="plus";text="请输入包名如：string.text";left=414;top=49;right=564;bottom=79;align="left";color=255;z=2};
textPath={cls="plus";text="   ";left=414;top=184;right=564;bottom=214;align="left";color=255;z=1};
textVersion={cls="plus";text="请输入版本号如：v0.0.1";left=414;top=92;right=564;bottom=122;align="left";color=255;z=5}
)
/*}}*/

import style
import package;
import config;
import  fsys.dlg
import inet.url
import win.dlg.message;
var libs = package.loadCacheLib()

import win.debounce
switchPlugIcon = function(plus,status){
	if(status){
		plus.iconText='\uF05D'
		plus.iconColor=0xFF54A878
	}else{
		plus.iconText='\uF05E'
		plus.iconColor=0xFFFF0000
	}
}



winform.editLibName.onStateChange =  function(hFocus){
	var libName = winform.editLibName.text
	var status = !libs[libName]
	winform.textLibName.text=""
	if(!status){
		winform.textLibName.text="包名冲突"
	}
	if(#libName==0){
		status = false; 
		winform.textLibName.text="包名不能为空"
	} 
	switchPlugIcon(winform.editLibName,status)
}
winform.libVersion.onStateChange = function(state){
	var version = winform.libVersion.text
	winform.textVersion.text=""
	var status = true
	if(#version==0){
		status = false; 
		winform.textVersion.text="版本号不能为空"
	} 
	switchPlugIcon(winform.libVersion,status)
}
var comBoxList = {'url','git','path'}

winform.combobox.onDropDown= function(){ 
	winform.combobox.autoComplete(comBoxList) //更新下拉列表
}

var removeCls = function(){
	winform.remove("textHint","btnOpenFile","editPath")
}

//文件路径安装
var addFileCls = function(){
	removeCls()
	winform.add(
		btnFileOpen={cls="plus";left=94;top=185;right=124;bottom=211;align="left";border={left=1;top=1;bottom=1;color=-16711681};iconColor=11580047;iconStyle={align="right";font=LOGFONT(h=-19;name='FontAwesome');padding={top=2;right=5}};iconText='\uF07B';notify=1;tabstop=1;textPadding={left=5;top=5};z=11}; 
		editPath={cls="plus";left=124;top=185;right=404;bottom=211;align="left";border={top=1;bottom=1;right=1;color=-16711681};editable=1;iconColor=255;iconStyle={align="right";font=LOGFONT(h=-19;name='FontAwesome');padding={top=2;right=5}};iconText='\uF05E';notify=1;tabstop=1;textPadding={left=5;top=5;right=25};z=12};
		textHint={cls="plus";text="文件路径：";left=9;top=185;right=87;bottom=215;align="right";color=16776960;z=12};
	)
	winform.btnFileOpen.skin({
		background={
			hover=0xFF928BB3;
			disabled=0xFFCCCCCC; 
		}
	})
	winform.btnFileOpen.oncommand = function(id,event){
		//打开文件
		var dir=fsys.dlg.open("aardio 文件或者库目录|*.aardio","选择aardio 文件或者库目录","/",winform,)
		if(!dir){
			winform.msgWarn("取消打开")
		}
		winform.editPath.text = dir
	}
	winform.editPath.onStateChange = function(){
		var path = winform.editPath.text
		winform.textPath.text=""
		if(#path>0){
			path =  ..string.replace(path,"\\","/")
			winform.editPath.text=path
		}
		if(!io.exist(path)){
			winform.textPath.text="文件路径错误"
		}
		switchPlugIcon(winform.editPath,io.exist(path))

	}
	
}
//git 安装
var addGitCls = function(){
	removeCls()
	winform.add( 
		editPath={cls="plus";left=94;top=185;right=404;bottom=211;align="left";border={color=-16711681;width=1};editable=1;iconColor=255;iconStyle={align="right";font=LOGFONT(h=-19;name='FontAwesome');padding={top=2;right=5}};iconText='\uF05E';notify=1;tabstop=1;textPadding={left=5;top=5;right=25};z=11};
		textHint={cls="plus";text="仓库地址：";left=9;top=185;right=87;bottom=215;align="right";color=16776960;z=12};
	)
	winform.editPath.onStateChange = function(state){
		var url  = winform.editPath.text
		var status = true
		winform.textPath.text=""
		if(!inet.url.is(url,0x0/*_URLIS_URL*/)){
			status = false
			winform.textPath.text="不是url"
		}
		switchPlugIcon(winform.editPath,status)
	}
}
//url 下载
var addUrlCls = function(){
	removeCls()
	winform.add( 
		editPath={cls="plus";left=94;top=185;right=404;bottom=211;align="left";border={color=-16711681;width=1};editable=1;iconColor=255;iconStyle={align="right";font=LOGFONT(h=-19;name='FontAwesome');padding={top=2;right=5}};iconText='\uF05E';notify=1;tabstop=1;textPadding={left=5;top=5;right=25};z=11};
		textHint={cls="plus";text="包网址：";left=9;top=185;right=87;bottom=215;align="right";color=16776960;z=12};
	)
	winform.editPath.onStateChange = function(state){
		var url  = winform.editPath.text
		winform.textPath.text=""
		var status = true
		if(!inet.url.is(url,0x0/*_URLIS_URL*/)){
			status = false
			winform.textPath.text="不是url"
			return ; 
		}
		if(!string.match(url,"<.gz>|<.tar>|<.tgz>|<lzma>$")){
			status = false
			winform.textPath.text="只支持.gz .tgz .tar .lzma"
		}
		switchPlugIcon(winform.editPath,status)
	}
}


winform.combobox.onListChange= function(){ 
	var selectText = winform.combobox.selText
	select(selectText) {
		case 'url' {
			addUrlCls()
		}
		case 'git' {
			addGitCls()
		}
		case 'path'{
			..publish("log","选择 aardio 文件，如果库为目录那么选择库的_.aardio 文件")
			addFileCls()
		}
	}
}

var installPackage = function(installPath){
	
	if(#winform.textLibName.text>0 || #winform.textVersion.text>0 || #winform.textPath.text>0){
		return   winform.msgErr("输入有误");
	}
	var libName = winform.editLibName.text
	var libClass = winform.combobox.text
	if(#libClass==0) return winform.msgErr("未选择包类型",1000);
	var libInfo = {
			version = winform.libVersion.text;
			[libClass] = winform.editPath.text;
	}
	import string.toml
	var test=string.toml.stringify(libInfo)
	
	var err = package.installLib(libName,libInfo,installPath)
	if(#err>0){
		return winform.msgErr(err); 
	}
	package.writConifg(libName,libInfo)
	winform.msgOk(libName+"安装成功")
	ide.refreshStdLib()
	ide.refreshUserLib()
}
winform.btnGlobalInsall.skin(style.primaryButton)
winform.btnGlobalInsall.oncommand = function(id,event){
	winform.btnGlobalInsall.disabled = true;
	var oldConfigPath = config.toml.path
	config.toml.path = package._global_config_path
	installPackage(package._global_install_path)	
	config.toml.path = oldConfigPath
	winform.btnGlobalInsall.disabled = false;
}

winform.btnProjectInstall.skin(style.primaryButton)
winform.btnProjectInstall.oncommand = function(id,event){
	winform.btnProjectInstall.disabled = true;
	var oldConfigPath = config.toml.path
	config.toml.path = package._project_config_path
	installPackage(package._project_install_path)	
	config.toml.path = oldConfigPath
	winform.btnProjectInstall.disabled = false;
}


winform.show();
win.loopMessage();
return winform;